.. index:: ! constant

.. _constants:

*****************************************
Variables d'état constantes et immutables
*****************************************

Les variables d'état peuvent être déclarées comme ``constantes`` ou ``immutable``. Dans les deux cas, ces variables ne peuvent être modifiées après la construction du contrat.
Dans ce cas, elles doivent être assignées à partir d'une expression constante au moment de la compilation.
Pour les variables ``constant``, la valeur doit être connue à la compilation.
Pour les variables ``immutable``, les variables peuvent être assognées jusqu'à la construction.

It is also possible to define ``constant`` variables at the file level.

Le compilateur ne réserve pas d'emplacement de stockage pour ces variables, et chaque occurrence est remplacée par l'expression constante correspondante.

Compared to regular state variables, the gas costs of constant and immutable variables
are much lower. For a constant variable, the expression assigned to it is copied to
all the places where it is accessed and also re-evaluated each time. This allows for local
optimizations. Immutable variables are evaluated once at construction time and their value
is copied to all the places in the code where they are accessed. For these values,
32 bytes are reserved, even if they would fit in fewer bytes. Due to this, constant values
can sometimes be cheaper than immutable values.

Tous les types de constantes ne sont pas implémentés pour le moment. Les seuls types pris en charge sont
`chaines de caractères <strings>`_ (uniquement pour les constantes) et `types valeur <value-types>`_.

.. code-block:: solidity

    // SPDX-License-Identifier: GPL-3.0
    pragma solidity >=0.7.4;

    uint constant X = 32**22 + 8;

    contract C {
        string constant TEXT = "abc";
        bytes32 constant MY_HASH = keccak256("abc");
        uint immutable decimals;
        uint immutable maxBalance;
        address immutable owner = msg.sender;

        constructor(uint _decimals, address _reference) {
            decimals = _decimals;
            // L'assignement à des immutables peut même accéder à l'environnement
            maxBalance = _reference.balance;
        }

        function isBalanceTooHigh(address _other) public view returns (bool) {
            return _other.balance > maxBalance;
        }
    }


Constant
========

Pour les variables ``constantes``, les valeurs doivent être assignées à partir d'une expression constante au moment de la
compilation et doit être assignée à la déclaration. Toute expression qui accède au stockage, aux données de la blockchain
(par exemple ``address(this).balance`` ou ``block.number``) ou
les données d'exécution (``msg.value`` ou ``gasleft()``) ou les appels vers des contrats externes sont interdits.
Les expressions qui peuvent avoir un effet secondaire sur l'allocation de mémoire sont autorisées,
mais celles qui peuvent avoir un effet secondaire sur d'autres objets mémoire ne le sont pas.
Les fonctions intégrées ``keccak256``, ``sha256``, ``ripemd160``, ``ecrecover``, ``addmod`` et ``mulmod`` sont autorisées (même si des contrats externes sont appelés).

La raison pour laquelle on autorise les effets secondaires sur l'allocateur de mémoire est qu'il devrait être possible de construire des objets complexes comme par exemple des tables de consultation.
Cette fonctionnalité n'est pas encore entièrement utilisable.

Immutable
=========

Variables declared as ``immutable`` are a bit less restricted than those
declared as ``constant``: Immutable variables can be assigned an arbitrary
value in the constructor of the contract or at the point of their declaration.
They can be assigned only once and can, from that point on, be read even during
construction time.

The contract creation code generated by the compiler will modify the
contract's runtime code before it is returned by replacing all references
to immutables by the values assigned to the them. This is important if
you are comparing the
runtime code generated by the compiler with the one actually stored in the
blockchain.

.. note::
  Immutables that are assigned at their declaration are only considered
  initialized once the constructor of the contract is executing.
  This means you cannot initialize immutables inline with a value
  that depends on another immutable. You can do this, however,
  inside the constructor of the contract.

  This is a safeguard against different interpretations about the order
  of state variable initialization and constructor execution, especially
  with regards to inheritance.